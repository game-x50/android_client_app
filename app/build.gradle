apply from: rootProject.ext[GradleExtraArgs.baseAndroidApp]

apply from: rootProject.ext[GradleExtraArgs.dagger2Kapt]
apply from: rootProject.ext[GradleExtraArgs.viewBinding]
apply from: rootProject.ext[GradleExtraArgs.applicationLeakCanaryTool]

apply from: rootProject.ext[GradleExtraArgs.apkSigning]

apply from: rootProject.ext[GradleExtraArgs.projectPropertiesRead]

android {

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId = "com.ruslan.hlushan.game"
        versionCode = 1
        versionName = "1.0"

        buildConfigField("String", "DEFAULT_LANGUAGE_NON_FULL_CODE", "\"${ApplicationConfigs.defaultLanguageNonFullCode}\"")
        buildConfigField("String[]", "AVAILABLE_LANGUAGES_FULL_CODES", "{" + ApplicationConfigs.availableLanguagesFullCodes.collect {
            "\"${it}\""
        }.join(",") + "}")

        buildConfigField("String", "BASE_API_URL", getProjectProperty("BASE_API_URL"))

        resConfigs(ApplicationConfigs.applicationLanguagesNonFullCodes)

        multiDexEnabled = true
    }

    buildTypes {
        debug {
            buildConfigField("String", "APP_TAG", "\"GAME_APP_DEBUG\"")
            buildConfigField("boolean", "IS_LOGCAT_ENABLED", "true")
            buildConfigField("boolean", "IS_CRASHLYTICS_ENABLED", "false")

            versionNameSuffix = "-${BuildTypes.debug}"
            applicationIdSuffix = ".${BuildTypes.debug}"

            signingConfig = signingConfigs.debug
        }

        release {
            proguardFiles(getDefaultProguardFile(ApplicationConfigs.defaultProguardFile))
            proguardFiles(fileTree(rootProject.ext[GradleExtraArgs.proguardConfigsFolder]).asList().toArray())

            buildConfigField("String", "APP_TAG", "\"GAME_APP_RELEASE\"")
            buildConfigField("boolean", "IS_LOGCAT_ENABLED", "false")
            buildConfigField("boolean", "IS_CRASHLYTICS_ENABLED", "true")

            signingConfig = signingConfigs.release
        }

        staging {
            initWith(buildTypes.release)

            versionNameSuffix = "-${BuildTypes.staging}"
            applicationIdSuffix = ".${BuildTypes.staging}"

            signingConfig = signingConfigs.debug
        }
    }

    resourcePrefix("game_app_")
}

dependencies {
    implementation(project(LocalGradleProjects.extensions))
    implementation(project(LocalGradleProjects.androidExtensions))
    implementation(project(LocalGradleProjects.coreApi))
    implementation(project(LocalGradleProjects.androidCoreApi))
    implementation(project(LocalGradleProjects.coreImpl))

    implementation(project(LocalGradleProjects.parsingImpl))

    implementation(project(LocalGradleProjects.coreUiApi))
    implementation(project(LocalGradleProjects.coreUiImpl))

    compileOnly(project(LocalGradleProjects.networkApi))//todo: compileOnly here instead of network_impl module because of some used extensions in network_impl, try to use implementation here and compileOnly in network_impl module
    implementation(project(LocalGradleProjects.networkImpl))

    implementation(project(LocalGradleProjects.storage))

    implementation(project(LocalGradleProjects.workManagerExtensions))

    implementation(project(LocalGradleProjects.gameCoreApi))

    implementation(project(LocalGradleProjects.gameAuthImpl))
    implementation(project(LocalGradleProjects.gameStorageImpl))
    implementation(project(LocalGradleProjects.gameTopImpl))

    implementation(project(LocalGradleProjects.gameAuthUi))
    implementation(project(LocalGradleProjects.gamePlayUi))
    implementation(project(LocalGradleProjects.gameSettingsUi))
    implementation(project(LocalGradleProjects.gameTopUi))

    compileOnly(Deps.rxJava2)//todo: compileOnly here instead of core_impl module of because ot some dependencies conflicts in core_impl, try to use implementation here and compileOnly in core_impl module
    runtimeOnly(project(LocalGradleProjects.rxjava2Extensions))

    runtimeOnly(project(LocalGradleProjects.threeTenExtensions))
    implementation(Deps.threeTenBpAndroid)

    implementation(Deps.supportAppCompat)
    implementation(Deps.supportDesign)
    runtimeOnly(Deps.swipeRefreshLayout)
    runtimeOnly(Deps.constraintLayout)
    runtimeOnly(Deps.recyclerView)

    implementation(Deps.edgeToEdgeDecorator)

    implementation(Deps.dagger2)

    implementation(Deps.cicerone)

    implementation platform(Deps.firebaseBom)
    runtimeOnly(Deps.firebaseAnalytics)//todo because of firebaseAuth, crashlytics require
    implementation(Deps.firebaseCrashlytics)
}

apply plugin: Plugins.googleServices

apply plugin: Plugins.crashlytics
android {
    buildTypes {
        debug {
            firebaseCrashlytics.mappingFileUploadEnabled = false
        }
        staging {
            firebaseCrashlytics.mappingFileUploadEnabled = true
        }
        release {
            firebaseCrashlytics.mappingFileUploadEnabled = true
        }
    }
}